#!/usr/bin/env perl

use strict;
use warnings;
use lib '/home/camp/lib';
use Cwd;
use Camp::Master;

my $camp;

if (@ARGV and $ARGV[0] =~ /\A\d+\z/) {
    # first command-line argument can be camp number
    $camp = shift;
}

unless (defined $camp and $camp =~ /\A\d+\z/) {
    # if not specified, get current camp # from current working directory
    my $dir = getcwd();
    die "current working directory doesn't exist!\n" unless $dir;
    $dir =~ m{/camp(\d+)} and $camp = $1;
}

unless (defined($camp)) {
    die "Please specify a camp #\n";
}

initialize(
    camp => $camp,
);

my $conf = config_hash();
my $port = $conf->{db_port};

# setup environment
if ($conf->{db_type} eq 'pg') {
    $ENV{PGUSER}     ||= $conf->{db_default_user};
    $ENV{PGDATABASE} ||= $conf->{db_default_database};
    $ENV{PGPORT}     ||= $port;
}
else {
    # mysql/other-specific variable initialization
}

$ENV{PERL5LIB} .= ':' if defined $ENV{PERL5LIB} && $ENV{PERL5LIB} ne '';
$ENV{PERL5LIB} .= "$ENV{HOME}/camp$camp/interchange/custom/lib:$ENV{HOME}/camp$camp/interchange/lib";
$ENV{PATH}     = "$ENV{PATH}:$ENV{HOME}/camp$camp/bin";
$ENV{CAMP}     = $camp;

exec @ARGV;


=pod

=head1 NAME

with-camp - wrapper to run another command with camp awareness

=head1 VERSION

3.02

=head1 DESCRIPTION

This utility sets necessary environment variables so that you can use
pg_dump and similar programs without needing a separate wrapper like
psql_camp for each. In fact psql_camp could be replaced with C<with-camp psql>.

=head1 AUTHOR

David Christensen E<lt>david@endpoint.comE<gt> and other contributors

=head1 LICENSE AND COPYRIGHT

Copyright (C) 2006-2011 End Point Corporation, http://www.endpoint.com/

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program. If not, see: http://www.gnu.org/licenses/

=cut
