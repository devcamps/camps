#!/usr/local/bin/perl

use strict;
use warnings;
use Getopt::Long;
use Cwd;
use lib '/home/camp/lib';
use Camp::Master;

*STDOUT->autoflush(1);
*STDERR->autoflush(1);

die "Don't run this as root!\n" if $> == 0;

my %conf;
GetOptions(
    \%conf,
    qw(
        stop
        start
        restart
        all
        user
    )
);

my @camp_numbers;
my $all_user_camps = $conf{user};

@camp_numbers = grep { m/\A\d+\z/ } @ARGV;

if ($all_user_camps) {
    @camp_numbers =
        map { $_->{camp_number} }
        grep { $_->{username} eq $ENV{USER} }
        Camp::Master::camp_list( all => 1)
    ;
}

unless (@camp_numbers) {
    # if not specified, get current camp # from current working directory
    (getcwd) =~ m{/camp(\d+)} and @camp_numbers = $1;
}

unless (@camp_numbers) {
    die <<'EOF';
camp restart script

Usage: re [--stop|start|restart] [--all] [<number>+ | --user ]

Default action is --restart.

By default only the Rails/Interchange servers are acted on.
Add the --all option to also act on PostgreSQL and Apache.

Camp number is inferred from a camp's current working directory if available.

If the --user flag is passed, all of the user's camps are affected.

Example: re 44
EOF
}


for my $camp_number (@camp_numbers) {
    initialize(
        force => 1,
        camp => $camp_number,
    );

    die "You are not the owner of camp $camp_number!\n"
        if $< != camp_user_obj()->uid()
    ;

    $conf{restart} = 1 unless $conf{stop} or $conf{start};

    my $action;
    for (qw( restart stop start )) {
        $action = $_, last if $conf{$_};
    }

    if ($conf{all}) {
        server_control(
            action  => $action,
            service => 'all',
        );
    }
    else {
        if (has_rails()) {
            server_control(
                action  => $action,
                service => 'rails',
            );
        }
        if (has_ic()) {
            server_control(
                action  => $action,
                service => 'ic',
            );
        }
    }
}

exit 0;


=pod

=head1 NAME

re - start/stop/restart one or more camp services

=head1 VERSION

3.00

=head1 AUTHOR

Ethan Rowe E<lt>ethan@endpoint.comE<gt> and other contributors

=head1 LICENSE AND COPYRIGHT

Copyright (C) 2006-2009 End Point Corporation, http://www.endpoint.com/

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program. If not, see: http://www.gnu.org/licenses/

=cut
