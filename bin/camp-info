#!/usr/local/bin/perl

use strict;
use warnings;

use lib '/home/camp/lib';

use Camp::Master;
use Cwd;
use Getopt::Long;

*STDOUT->autoflush(1);
*STDERR->autoflush(1);

die "Don't run this as root!\n" if $< == 0;

my (@attribute_list, %opt, @all_fields, @requested_fields);

# populate initial attributes for Getopt based on lookup table.
my %queryable_attributes = (
    camp_number => 'get',
    camp_type   => 'get',
    vcs_type    => 'get',
    username    => 'get',
    comment     => 'getset',
);

my %attribute_aliases = (
    number => 'camp_number',
    num    => 'camp_number',
    type   => 'camp_type',
    name   => 'username',
    vcs    => 'vcs_type',
);

my %set_synonyms = (
    comment => 'm',
);


@all_fields = sort keys %queryable_attributes;

while (my ($attribute, $type) = each %queryable_attributes) {
    my @gets = $attribute;

    # construct a list of aliases
    while (my ($alias, $aliased_attribute) = each %attribute_aliases) {
        push @gets, $alias if $aliased_attribute eq $attribute;
    }

    my @sets = map { "set-$_" } @gets;

    for ($set_synonyms{$attribute}) {
        push @sets, $_ if $_;
    }

    my $get_clause = join "|", @gets;
    my $set_clause = join "|", @sets;

    push @attribute_list, $get_clause     if $type =~ /get/;
    push @attribute_list, "$set_clause=s" if $type =~ /set/;
}

my $USAGE=<<EOH;
Usage: camp-info [options] [number]

If the camp number is not specified, it may be inferred from the CAMP
environment variable or the current working directory.

Options:
--all, -a          Show the camp information for all camps for this user.
--comment          Show the comment for the camp.
--set-comment, -m  Set the comment for the camp.
--number           Show the camp number for the camp.
--type             Show the camp type for the camp.
--vcs              Show the version control system in use for the camp.
--quiet, -q        Suppress the display of informational headers.
--no-newline, -n   Suppress the display of the newline in the output.
--separator, -s    Separate output with a specified string.
--help, -h         Show this help.
EOH

GetOptions(
    \%opt,
    "help|h",
    "quiet|q",
    "no-newline|n",
    "separator|sep=s",
    "all|a",
    @attribute_list,
) or die $USAGE;

if ($opt{help}) {
    print $USAGE;
    exit;
}

my $display_output_headers = ! $opt{quiet};
my $camp_number = resolve_camp_number(shift);

if (!defined $camp_number) {
    exit 1 if $opt{quiet};
    die "$0: couldn't determine camp; specify in command line or CAMP environment variable\n";
}

if ($opt{all}) {
    my $username = getpwuid($<);
    my $info = Camp::Master::get_all_camp_info( $username );
    print $info;
    exit;
}

my $info = Camp::Master::get_camp_info( $camp_number );
my $set_attribute;

# TODO: make the order of the attribute flags passed match the order of the output
foreach my $attribute (keys %queryable_attributes) {
    if ($opt{$attribute}) {
        push @requested_fields, $attribute;
    }
    if ($opt{"set-$attribute"}) {
        # TODO: generalize this; only one settable attribute now, so we're weenie-ing out.
        warn "set-$attribute not supported" and next unless $attribute eq 'comment';

        $set_attribute = 1;

        # TODO: check user privs to change the camp...
        # update the comment field;
        Camp::Master::set_camp_comment($camp_number, $opt{'set-comment'});
    }
}

@requested_fields = @all_fields unless @requested_fields || $set_attribute;

# actual output based on passed params
my $sep = $opt{separator} || qq{\n};

if (!$set_attribute) {
    print join $sep,
        map {
            ($display_output_headers ? "$_: " : '')
            . $info->{$_}
        } @requested_fields;
    print "\n" unless $opt{'no-newline'};
}


=pod

=head1 NAME

camp-info - camp information getter/setter

=head1 VERSION

3.00

=head1 AUTHOR

Ethan Rowe E<lt>ethan@endpoint.comE<gt> and other contributors

=head1 LICENSE AND COPYRIGHT

Copyright (C) 2006-2009 End Point Corporation, http://www.endpoint.com/

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program. If not, see: http://www.gnu.org/licenses/

=cut
