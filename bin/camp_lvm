#!/usr/bin/perl

# This script manages all LVM related tasks for camps
# This should be run as root via sudo.

use strict;
use warnings;
use Getopt::Long;
use lib '/home/camp/lib';
use Camp::Master;
use Camp::LVM;
use Data::Dumper;

*STDOUT->autoflush(1);
*STDERR->autoflush(1);

die "Must run this as root!\n" if $< != 0;


my %opt;
GetOptions(
    \%opt,
    qw(
        number|n=i
        username|u=s
        help|h
    )
);
my $action = shift @ARGV;
#print Dumper(\%opt), $/;  ## DEBUG

show_usage()                    if ($opt{help});
show_usage("Missing action.")   unless ($action);
my @available_actions = ('create','remove','resize','resize_all');
show_usage("Action '$action' not available.")  unless (grep { $action eq $_ } @available_actions);

if ( $action =~ /^(?:create|remove|resize)$/ ) {
    show_usage("Missing username.") unless ($opt{username});
    show_usage("Missing number.")   unless (defined $opt{number});

    # Load the camp info
    initialize(
	force => 1,
	camp => $opt{number},
    );

    # Check that the given username owns the given camp number
    die "Camp number $opt{number} is not owned by username $opt{username}!"  if (camp_user() ne $opt{username});
}


if ( $action eq 'create' ) {
    Camp::LVM::clone_database(config_hash());
}
elsif ( $action eq 'remove' ) {
    Camp::LVM::remove_database_clone(config_hash());
}
elsif ( $action eq 'resize' ) {
    Camp::LVM::resize_snapshots(config_hash());
}
elsif ( $action eq 'resize_all' ) {
    Camp::LVM::resize_snapshots({});
}



#-------------------------------

sub show_usage {
    my $msg = shift || '';
    print $msg. "\n"  if ($msg);
    print <<EOH;
Usage:
  camp_lvm create -n|--number=XX -u|--username=USERNAME
  camp_lvm remove -n|--number=XX -u|--username=USERNAME
  camp_lvm resize -n|--number=XX -u|--username=USERNAME
  camp_lvm resize_all

Options:

-u USERNAME
--username=USERNAME
    Specify the username of the owner of this camp.

-n XX
--number=XX
    Specify the camp number for this camp.

--help      This help screen

Action of 'resize' will increase the size of a camp's
snapshot if it's COW table usage is higher than a
threshold- default is 50% used.  The action of 'resize_all'
does this for all camps.

EOH
    exit;
}
